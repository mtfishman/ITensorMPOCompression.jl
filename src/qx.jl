using LinearAlgebra
@doc """
  `block_qx(W::ITensor,ul::reg_form)::Tuple{ITensor,ITensor,Index}`

Perform a block respecting QX decomposition of the operator valued matrix `W`. 
The appropriate decomposition, QR, RQ, QL, LQ is selected based on the `reg_form` `ul` 
and the `orth` keyword argument.
The new internal `Index` between Q and R/L is modified so that the tags are "Link,qx" instead
"Link,qr" etc. returned by the qr/rq/ql/lq routines.  Q and R/L are also gauge fixed so that 
the corner element of R/L is 1.0 and Q⁺Q=d𝕀 where d is the dimensionality of the local
Hilbert space.

# Arguments
- `W` Operator valued matrix for decomposition.
- `ul` upper/lower regular form of `W`. We can auto detect here, but is more efficient if this is done by the higher level calling routines.

# Keywords
- `orth::orth_type = right` : choose `left` or `right` orthogonal (canonical) form
- `rr_cutoff::Float64 = -1.0` : cutoff for rank revealing QX which removes zero pivot rows and columns. 
   All rows with max(abs(R[r,:]))<rr_cutoff are considered zero and removed.  rr_cutoff==-1.0 indicates no rank reduction.

# Returns a Tuple containing
- `Q` with orthonormal columns or rows depending on orth=left/right, dimensions: (χ+1)x(χq+1)
- `R` or `L` depending on `ul` with dimensions: (χq+2)x(χ\'+2)
- `iq` the new internal link index between `Q` and `R`/`L` with dimensions χq+2 and tags="Link,qx"

# Example
```julia
julia>using ITensors, ITensorMPS
julia>using ITensorMPOCompression
julia>N=5; #5 sites
julia>NNN=2; #Include 2nd nearest neighbour interactions
julia>sites = siteinds("S=1/2",N);
#
#  Make a Hamiltonian directly, i.e. not using autoMPO
#
julia>H=transIsing_MPO(sites,NNN);
#
#  Use pprint to see the structure for site #2. I = unit operator and S = any 
#  other operator
#
julia>pprint(H[2]) #H[1] is a row vector, so let's see what H[2] looks like
I 0 0 0 0 
S 0 0 0 0 
S 0 0 0 0 
0 0 I 0 0 
0 S 0 S I 
#
#  Now do a block respecting QX decomposition. QL decomposition is chosen because
#  H[2] is in lower regular form and the default ortho direction is left.
#
julia>Q,L,iq=block_qx(H[2];rr_cutoff=1e-14); #Block respecting QL
#
#  The first column of Q is unchanged because it is outside the V-block.
#  Also one column was removed because set rr_cutoff to enable rank revealing QX.
#
julia>pprint(Q)
I 0 0 0 
S 0 0 0 
S 0 0 0 
0 I 0 0 
0 0 S I 
#
#  Similarly L is missing one row due to the rank revealing QX algorithm
#
julia>pprint(L,iq) #we need to tell pprint the iq is the row index.
I 0 0 0 0 
0 0 I 0 0 
0 S 0 S 0 
0 0 0 0 I 
```
"""

function equal_edge_blocks(i1::QNIndex, i2::QNIndex)::Bool
  qns1, qns2 = space(i1), space(i2)
  qn11, qn1n = qns1[1], qns1[nblocks(qns1)]
  qn21, qn2n = qns2[1], qns2[nblocks(qns2)]
  return ITensors.have_same_qns(qn(qn11), qn(qn21)) &&
         ITensors.have_same_qns(qn(qn1n), qn(qn2n))
end

function equal_edge_blocks(::Index, ::Index)::Bool
  return true
end



function insert_Q(Ŵrf::reg_form_Op, Q̂::ITensor, iq::Index, lr::orth_type)
  @mpoc_assert Ŵrf.ul==lower
  #
  #  Create new index by growing iq.
  #
  ilb, ilf = linkinds(Ŵrf, lr) #Backward and forward indices.
  iq⎖ = redim(iq, 1, 1, space(ilf))  #pad with 1 at the start and 1 and the end: iqp =(1,iq,1).
  ileft, iright = lr == left ? (ilb, iq⎖) : (iq⎖, ilb)
  #
  #  Create a new reg form tensor
  #
  Ŵrf⎖ = reg_form_Op(eltype(Ŵrf), ileft, iright,siteinds(Ŵrf))
  #
  #  Preserve b,c,d blocks and insert Q
  #
  Wb1 = extract_blocks(Ŵrf, lr; b=true, c=true, d=true) 
  set_𝐛̂𝐜̂_block!(Ŵrf⎖, Wb1, lr) #preserve b or c block from old W
  set_𝐝̂_block!(Ŵrf⎖, Wb1.𝐝̂) #preserve d block from old W
  set_𝕀_block!(Ŵrf⎖, Wb1.𝕀) #init I blocks from old W
  set_𝐀̂𝐜̂_block(Ŵrf⎖, Q̂, lr) #Insert new Qs form QR decomp

  return Ŵrf⎖, iq⎖
end

function ac_qx(Ŵrf::reg_form_Op, lr::orth_type; qprime=false, verbose=false, cutoff=1e-14, kwargs...)
  @mpoc_assert Ŵrf.ul==lower
  @checkflux(Ŵrf.W)
  Wb = extract_blocks(Ŵrf, lr; Ac=true)
  𝐀̂𝐜̂=Wb.𝐀̂𝐜̂
  ilf_Ac =forward(𝐀̂𝐜̂,lr)
  ilf = forward(Ŵrf, lr) #Backward and forward indices.
  @checkflux(𝐀̂𝐜̂.W)
  if lr == left
    Qinds = noncommoninds(𝐀̂𝐜̂.W, ilf_Ac)
    Q̂, R, iq, Rp = qr(
      𝐀̂𝐜̂.W, Qinds; verbose=verbose, positive=true, atol=cutoff, tags=tags(ilf), kwargs...
    )
  else
    Rinds = ilf_Ac
    R, Q̂, iq, Rp = ITensors.lq(
      𝐀̂𝐜̂.W, Rinds; verbose=verbose, positive=true, atol=cutoff, tags=tags(ilf), kwargs...
    )
  end
  @checkflux(Q̂)
  @checkflux(R)
  # Re-scale
  dh = d(Wb) #dimension of local Hilbert space.
  @assert abs(dh - round(dh)) == 0.0 #better be an integer!
  Q̂ *= sqrt(dh)
  R /= sqrt(dh)

  Ŵrf⎖, iq⎖ = insert_Q(Ŵrf, Q̂, iq, lr) #create a new W with Q.  The size may change.
  @assert equal_edge_blocks(ilf, iq⎖)

  #both inds or R have the same tags, so we prime one of them so the grow function can distinguish.
  R⎖ = grow(prime(R, iq), dag(iq⎖)', ilf)
  if !isnothing(Rp)
    Rp /= sqrt(dh)
    Rp⎖=grow(prime(Rp, iq), dag(iq⎖)', ilf)
  else
    Rp⎖=nothing
  end
  if qprime
    iq⎖ = prime(iq⎖)
  else
    R⎖ = noprime(R⎖)
    if !isnothing(Rp)
      Rp⎖ = noprime(Rp⎖)
    end
  end
  return Ŵrf⎖, R⎖, iq⎖, Rp⎖
end
